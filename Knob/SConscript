import os

SConscript('Test/SConscript')

env = Environment(tools=['default', 'qt4'])
env['QWTDIR'] = os.environ.get('QWTDIR', None)
env.AppendUnique(CPPPATH=['$QWTDIR/include', '..', '$QTDIR/include/QtDesigner'])
env.AppendUnique(QT4_MOCFROMHFLAGS=['-I', '$QTDIR/include/QtDesigner/'])
env.EnableQt4Modules(['QtCore','QtGui',])

# uic knob form
env.Uic4('Knob.ui')

# build knob shared library
knob = env.SharedLibrary(
   'knob',
   ['Knob.cpp','KnobPlugin.cpp',],
   LIBS=['qwt',],
   LIBPATH=['$QWTDIR/lib',]
)

Default(knob)

installdir = os.path.join('$QTDIR','plugins','designer')
env.Install(installdir, knob)
env.Alias('install', installdir)



